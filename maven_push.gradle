/**
 * Copyright 2017 TENKIV, INC.

 Redistribution and use in source and binary forms, with or without modification, are permitted provided that the
 following conditions are met:

 1. Redistributions of source code must retain the above copyright notice, this list of conditions and the
 following disclaimer.

 2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following
 disclaimer in the documentation and/or other materials provided with the distribution.

 3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote
 products derived from this software without specific prior written permission.

 THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES,
 INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
 DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
 SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
 WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */
apply plugin: 'maven'
apply plugin: 'signing'

// This should only be true if you are wanting to upload your own artifact and have the prerequisite gradle properties.
// This will throw an error if it is flagged as 'true' and those properties are non-existent.
Properties getLocalProperties(){
    Properties localProperties = new Properties()
    localProperties.load(project.rootProject.file("local.properties").newDataInputStream())
    return localProperties
}

boolean isRelease() {
    if(project.rootProject.file("local.properties").exists())
        if(getLocalProperties().getProperty("release_flag"))
            true
        else
            false
    else
        false
}

def isSnapshotBuild() {
    return version.contains("SNAPSHOT") == true
}

uploadArchives {
    if (isRelease()) {
        def localProperties = getLocalProperties()
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.artifactId = POM_ARTIFACT_ID
                println(pom.artifactId)

                snapshotRepository(url: isSnapshotBuild() ?
                        "https://oss.sonatype.org/content/repositories/snapshots" :
                        mavenLocal().url) {
                    authentication(
                            userName: localProperties.getProperty('sonatype_user'),
                            password: localProperties.getProperty('sonatype_pass'))
                }

                repository(url: isRelease() ?
                        "https://oss.sonatype.org/service/local/staging/deploy/maven2/" :
                        mavenLocal().url) {
                    authentication(
                            userName: localProperties.getProperty('sonatype_user'),
                            password: localProperties.getProperty('sonatype_pass'))
                }

                pom.project {
                    name POM_NAME
                    packaging POM_PACKAGING
                    description POM_DESCRIPTION
                    url POM_URL

                    scm {
                        url POM_SCM_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_LICENCE_URL
                            distribution POM_LICENCE_DIST
                        }
                    }

                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                            email POM_DEVELOPER_EMAIL
                            organization POM_DEVELOPER_ORGANIZATION
                            organizationUrl POM_DEVELOPER_ORGANIZATION_URL
                        }
                    }
                }
            }
        }
    }
}

signing {
    required { isRelease() }
    if (isRelease()) {
        def localProperties = getLocalProperties()
        allprojects { ext."signing.keyId" = localProperties.getProperty('keyId') }
        allprojects { ext."signing.secretKeyRingFile" = localProperties.getProperty('secretKeyRingFile') }
        allprojects { ext."signing.password" = localProperties.getProperty('password') }

        sign configurations.archives
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}